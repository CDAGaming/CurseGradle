plugins {
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.0'
}

ext {
    inceptionYear = '2015'
    url = 'https://github.com/matthewprenger/CurseGradle'
    git = 'https://github.com/matthewprenger/CurseGradle.git'
}

version = '1.0.1-SNAPSHOT'
group = 'com.matthewprenger'
description = 'Gradle plugin to upload artifacts to CurseForge'
archivesBaseName = 'CurseGradle'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.4.1'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.4.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar.manifest.mainAttributes([
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version
])

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

assemble.dependsOn javadocJar, sourcesJar

[compileJava, compileGroovy].each {
    it.options.encoding = 'UTF-8'
    it.options.deprecation = true
    it.options.fork = true
}

pluginBundle {
    website = project.url
    vcsUrl = project.git
    plugins {
        curseGradlePlugin {
            id = 'com.matthewprenger.cursegradle'
            description = project.description
            displayName = 'CurseGradle'
            version = project.version
            tags = ['curseforge', 'publish', 'minecraft']
        }
    }
}

if (!version.endsWith('-SNAPSHOT')) {
    tasks.publish.dependsOn publishPlugins
}
