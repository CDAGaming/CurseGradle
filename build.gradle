plugins {
    id 'groovy'
    id 'idea'
    id 'signing'
    id 'maven'
    id 'com.gradle.plugin-publish' version '0.9.4'
}

ext {
    inceptionYear = '2015'
    url = 'https://github.com/matthewprenger/CurseGradle'
}

version = '1.0.8'
//version = '1.0-SNAPSHOT'
group = 'com.matthewprenger'
description = 'Gradle plugin to upload artifacts to CurseForge'
archivesBaseName = 'CurseGradle'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.2'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
}

jar.manifest.mainAttributes([
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version
])

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

assemble.dependsOn javadocJar, sourcesJar

[compileJava, compileGroovy].each {
    it.options.encoding = 'UTF-8'
    it.options.deprecation = true
    it.options.fork = true
}

signing {
    sign configurations.archives
}

uploadArchives {
    dependsOn build
    repositories {
        mavenDeployer {

            if (project.hasProperty('sonatypePassword')) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                    authentication(userName: 'matthewprenger', password: sonatypePassword)
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                    authentication(userName: 'matthewprenger', password: sonatypePassword)
                }
            } else {
                tasks.signArchives.enabled = false
                repository(url: "file://localhost/${project.file('repo').getAbsolutePath()}")
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
            }

            pom.project {
                name project.name
                description project.description
                packaging 'jar'
                url project.url
                inceptionYear project.inceptionYear

                scm {
                    url 'git@github.com:matthewprenger/CurseGradle.git'
                    connection 'scm:git:git@github.com:matthewprenger/CurseGradle.git'
                    developerConnection 'scm:git:git@github.com:matthewprenger/CurseGradle.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/matthewprenger/CurseGradle/issues'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'matthewprenger'
                        name 'Matthew Prenger'
                        roles {
                            role 'developer'
                        }
                    }
                }
            }
        }
    }
}

pluginBundle {
    website = project.url
    vcsUrl = 'git://github.com/matthewprenger/CurseGradle.git'
    plugins {
        curseGradlePlugin {
            id = 'com.matthewprenger.cursegradle'
            description = project.description
            displayName = 'CurseGradle'
            version = project.version
            tags = ['curseforge', 'publish', 'minecraft']
        }
    }
}

task release {
    dependsOn uploadArchives
    if (!project.version.endsWith('-SNAPSHOT')) {
        dependsOn publishPlugins
    }
}
