import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'groovy'
    id 'idea'
    id 'com.gradle.plugin-publish' version '1.0.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

ext {
    inceptionYear = '2015'
    url = 'https://github.com/CDAGaming/CurseGradle'
}

version = '1.5.4'
group = 'io.github.CDAGaming'
description = '(Updated Fork) Gradle plugin to upload artifacts to CurseForge'
archivesBaseName = 'CurseGradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()

    implementation group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    implementation group: 'com.google.guava', name: 'guava', version: '19.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.1'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
}

jar.manifest.mainAttributes([
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version
])

shadowJar {
    archiveClassifier.set('')
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "external"
}
tasks.shadowJar.dependsOn tasks.relocateShadowJar

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

assemble.dependsOn javadocJar, sourcesJar

[compileJava, compileGroovy].each {
    it.options.encoding = 'UTF-8'
    it.options.deprecation = true
    it.options.fork = true
}

pluginBundle {
    website = project.url
    vcsUrl = 'https://github.com/CDAGaming/CurseGradle'
    tags = ['curseforge', 'publish', 'minecraft']
}

gradlePlugin {
    plugins {
        curseGradlePlugin {
            id = 'io.github.CDAGaming.cursegradle'
            implementationClass = 'com.matthewprenger.cursegradle.CurseGradlePlugin'
            description = project.description
            displayName = 'CurseGradle'
            version = project.version
        }
    }
}