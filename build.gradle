plugins {
    id 'groovy'
    id 'idea'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

version = '1.4.0'
if (isNullOrEmpty(System.getenv("TRAVIS_TAG")) && !isNullOrEmpty(System.getenv('TRAVIS_BUILD_NUMBER'))) {
    version += '.' + System.getenv('TRAVIS_BUILD_NUMBER')
}
group = 'com.matthewprenger'
description = 'Gradle plugin to upload artifacts to CurseForge'
archivesBaseName = 'CurseGradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    implementation group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    implementation group: 'com.google.guava', name: 'guava', version: '19.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
}

jar.manifest.mainAttributes([
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version
])

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

assemble.dependsOn javadocJar, sourcesJar

[compileJava, compileGroovy].each {
    it.options.encoding = 'UTF-8'
    it.options.deprecation = true
    it.options.fork = true
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : System.getenv('bintray_user')
    key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : System.getenv('bintray_key')
    publish = true
    override = true
    publications = ['mavenJava']
    pkg {
        repo = "doublecart"
        name = "CurseGradle"
        user = bintray.user
        userOrg = "liachmodded"
        licenses = ["MIT"]
        desc = project.description
        websiteUrl = "https://github.com/liachmodded/CurseGradle"
        issueTrackerUrl = "https://github.com/liachmodded/CurseGradle/issues"
        vcsUrl = "https://github.com/liachmodded/CurseGradle.git"
        publicDownloadNumbers = true

        githubRepo = "liachmodded/CurseGradle"
        version {
            name = project.version
            released = new Date()
            if (!isNullOrEmpty(System.getenv("TRAVIS_TAG"))) {
                vcsTag = System.getenv("TRAVIS_TAG")
            } else {
                vcsTag = ''
            }
        }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

static boolean isNullOrEmpty(String st) {
    return st == null || st.isEmpty()
}
